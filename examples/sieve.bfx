include "std.bfx"

const MAX_INT_SIZE = 255;
const ARRAY_SIZE = 250;

function incr(&x, &p, &n)
{
    if (MAX_INT_SIZE - x < p) // would cause overflow
        x = n;
    else
        x += p;    // safe to increment
}

function main()
{
    println("Welcome to the BrainF*ck prime-sieve!");
    prints("Enter n (0-250): ");
    let n = scand();

    // Initialize array and calculate maximum sieve-value
    let [ARRAY_SIZE] arr = 1;
    let s = sqrt(n);
    
    // Sieve
    let prime = 2;
    while (prime <= s)
    {
        for (let i = prime^2; i < n; incr(i, prime, n)) // protect against overflow
            arr[i] = 0;

        ++prime;
        while (arr[prime] == 0)
            ++prime;
    }

    // Print results
    printPrimes(arr, n);
}

function printPrimes(&arr, n)
{
    let count = 0;
    for (let i = 2; i < n; ++i)
    {
        if (arr[i])
        {
            printd(i); endl();
            ++count;
        }
    }

    prints("Found "); printd(count); println(" primes!");
}
